plugins {
    id 'java'
    id("io.freefair.lombok") version "8.14"
    id("com.github.johnrengelman.shadow") version "7.1.0"
    id("org.springframework.boot") version "2.7.13"
    id("io.spring.dependency-management") version "1.0.15.RELEASE"
}

group = 'me.zypj'
version = '2.2beta'

repositories {
    mavenCentral()
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly files(
            "libs/spigot-1.8.8.jar",
    )
    compileOnly(
            'me.clip:placeholderapi:2.11.6',
            'org.bstats:bstats-bukkit:3.0.2'
    )

    implementation(
            'com.zaxxer:HikariCP:4.0.3',
            'com.github.ben-manes.caffeine:caffeine:2.9.3',
            'com.google.code.gson:gson:2.13.1',
            'org.springframework.boot:spring-boot-starter-web',
            'org.xerial:sqlite-jdbc:3.49.1.0'
    )
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set("")
    relocate 'com.github.benmanes.caffeine', 'me.zypj.revamp.lib.caffeine'
    relocate 'com.zaxxer.hikari', 'me.zypj.revamp.lib.hikari'
    relocate 'org.bstats', 'me.zypj.revamp.lib.bstats'
    relocate 'org.checkerframework', 'me.zypj.revamp.lib.org.checkerframework'
    relocate 'org.slf4j', 'me.zypj.revamp.lib.org.slf4j'
    relocate 'com.google.errorprone.annotations', 'me.zypj.revamp.lib.com.google.errorprone.annotations'
}